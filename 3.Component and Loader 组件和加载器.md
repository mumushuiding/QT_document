## Component and Loader 组件和加载器 

### QT Component (组件)

Component 是由Qt框架或开发者封装好的、只暴露了必要接口的QML类型，可以重复利用

一个Component既可以定义在独立的QML文件中，也可以嵌入到其他的QML文档中来定义

#### 嵌入式定义组件

在一个QML文档中嵌入Component的定义，需要使用Component对象

```
    Text {
        id: coloredText;
        text: qsTr("Hello world");
        anchors.horizontalCenter: parent.horizontalCenter;
        anchors.top: parent.top;
        anchors.topMargin: 4;
        font.pixelSize: 32;
    }
    // 以下为组件定义
    Component{
        id: colorC;
        Rectangle{
            id: colorPicker;
            width: 50;
            height: 30;
            signal colorPicked(color clr); // 定义了信号colorPicked
            MouseArea{
                anchors.fill: parent
                // 当鼠标点击时触发了colorPicked 信号
                onPressed: colorPicker.colorPicked(colorPicker.color);
            }
            // 信号处理器，每个继承该组件的Loader都会执行
            onColorPicked:{
                console.log("pick color")
            }
        }
    }
    // 以下为组件的使用
    Loader{
        id: redLoader;
        anchors.left: parent.left;
        anchors.leftMargin: 4;
        anchors.bottom: parent.bottom;
        anchors.bottomMargin: 4;
        sourceComponent: colorC;
        onLoaded: {
            item.color = "red";
        }
    }
    Loader{
        id: blueLoader;
        anchors.left: redLoader.right;
        anchors.leftMargin: 4;
        anchors.bottom: parent.bottom;
        anchors.bottomMargin: 4;
        sourceComponent: colorC;
        onLoaded: {
            item.color = "blue";
        }
    }
    // 通过connections对象接收信号
    Connections{
        target: redLoader.item;
        // 接收colorPicked信号
        onColorPicked:{
            console.log("pick red");
            coloredText.color=clr;
        }
    }
    Connections{
        target: blueLoader.item;
        onColorPicked:{
            console.log("pick blue");
            coloredText.color=clr;
        }

    }
```
#### 在单独文件中定义组件

在单独定义组件时不需要Component对象

文件名同组件id相同，且首字母必须大写

引用组件的文件和被引用的组件文件需要在同一个目录下

```
import QtQuick 2.12

Rectangle{
    id: colorPicker;
    width: 50;
    height: 30;
    signal colorPicked(color clr);

    function configureBorder(){
        colorPicker.border.width=colorPicker.focus?2:0;
        colorPicker.border.color=colorPicker.focus?"blue":"green";
    }

    MouseArea{
        anchors.fill: parent
        onClicked: {
            colorPicker.colorPicked(colorPicker.color);
            mouse.accepted=true;
            colorPicker.focus=true;
        }
    }
    onFocusChanged: {
        configureBorder();
    }

    Component.onCompleted: {
        configureBorder();
    }
}
```

### Loader

Loader 可以使用source属性加载一个QML文档,也可以通过sourceComponent属性加载一个Component对象

在需要延迟创建时，Loader非常有用

Loader加载新对象时或新对象为undefined时，旧对象会自动销毁

#### 从Component对象加载组件

```
    Text {
        id: coloredText;
        text: qsTr("Hello world");
        anchors.horizontalCenter: parent.horizontalCenter;
        anchors.top: parent.top;
        anchors.topMargin: 4;
        font.pixelSize: 32;
    }
    // 以下为组件定义
    Component{
        id: colorC;
        Rectangle{
            id: colorPicker;
            width: 50;
            height: 30;
            signal colorPicked(color clr); // 定义了信号colorPicked
            MouseArea{
                anchors.fill: parent
                // 当鼠标点击时触发了colorPicked 信号
                onPressed: colorPicker.colorPicked(colorPicker.color);
            }
            // 信号处理器，每个继承该组件的Loader都会执行
            onColorPicked:{
                console.log("pick color")
            }
        }
    }
    // 以下为组件的使用
    Loader{
        id: redLoader;
        anchors.left: parent.left;
        anchors.leftMargin: 4;
        anchors.bottom: parent.bottom;
        anchors.bottomMargin: 4;
        sourceComponent: colorC;
        onLoaded: {
            item.color = "red";
        }
    }
    // 以下为Loader的使用
    Loader{
        id: blueLoader;
        anchors.left: redLoader.right;
        anchors.leftMargin: 4;
        anchors.bottom: parent.bottom;
        anchors.bottomMargin: 4;
        sourceComponent: colorC;
        onLoaded: {
            item.color = "blue";
        }
    }
    // 通过connections对象接收信号
    Connections{
        target: redLoader.item;
        // 接收colorPicked信号
        onColorPicked:{
            console.log("pick red");
            coloredText.color=clr;
        }
    }
    Connections{
        target: blueLoader.item;
        onColorPicked:{
            console.log("pick blue");
            coloredText.color=clr;
        }

    }
```

####  从文件加载组件

```
Window {
    visible: true
    width: 640
    height: 480
    title: qsTr("Hello World")

    Text {
        id: coloredText;
        text: qsTr("Hello World");
        anchors.horizontalCenter: parent.horizontalCenter;
        anchors.top:parent.top;
        anchors.topMargin: 4;
        font.pixelSize: 32;
    }

    function setTextColor(clr){
        coloredText.color=clr;
    }
    Loader{
        id: redLoader;
        focus: true;
        anchors.left: parent.left;
        anchors.leftMargin: 4;
        anchors.bottom: parent.bottom;
        anchors.bottomMargin: 4;
        source: "ColorPicker.qml";
        onLoaded: {
            item.color="red";
        }
        onFocusChanged: {
            item.focus=focus;
        }
    }
    Connections{
        target: redLoader.item;
        onColorPicked:{
            coloredText.color=clr;
            if(!redLoader.focus){
                redLoader.focus=true;
                blueColor.focus=false;
            }
        }
    }

    ColorPicker{
        id: blueColor;
        color: "blue";
        anchors.left: redLoader.right;
        anchors.leftMargin: 4;
        anchors.bottom: parent.bottom;
        anchors.bottomMargin: 4;
        onColorPicked: {
            coloredText.color=clr
        }
    }
}
```

#### 利用Loader动态创建与销毁组件

```
Window {
    id: rootItem
    visible: true
    width: 640
    height: 480
    title: qsTr("Hello World")
    property var colorPickerShow: false

    Text {
        id: coloredText;
        text: qsTr("Hello World");
        anchors.horizontalCenter: parent.horizontalCenter;
        anchors.top:parent.top;
        anchors.topMargin: 4;
        font.pixelSize: 32;
    }
    Button{
        id: ctrlButton;
        text:"show";
        anchors.left:parent.left;
        anchors.leftMargin: 4;
        anchors.bottom: parent.bottom;
        anchors.bottomMargin: 4;

        onClicked: {
            if (rootItem.colorPickerShow){
                // 动态销毁
                // redLoader.sourceComponent=undefined;
                redLoader.source=""
                rootItem.colorPickerShow=false
                ctrlButton.text="show"
            }else{
                // 动态创建
                redLoader.source="ColorPicker.qml"
                redLoader.item.colorPicked.connect(setTextColor);
                redLoader.focus=true;
                rootItem.colorPickerShow=true;
                ctrlButton.text="Hide";
            }
        }
    }

    function setTextColor(clr){
        coloredText.color=clr;
    }
    Loader{
        id: redLoader;
        focus: true;
        anchors.left: ctrlButton.right;
        anchors.leftMargin: 4;
        anchors.bottom: parent.bottom;
        anchors.bottomMargin: 4;
        source: "ColorPicker.qml";
        onLoaded: {
            item.color="red";
        }
        onFocusChanged: {
            item.focus=focus;
        }
    }
    Connections{
        target: redLoader.item;
        onColorPicked:{
            coloredText.color=clr;
            if(!redLoader.focus){
                redLoader.focus=true;
                blueColor.focus=false;
            }
        }
    }

    ColorPicker{
        id: blueColor;
        color: "blue";
        anchors.left: redLoader.right;
        anchors.leftMargin: 4;
        anchors.bottom: parent.bottom;
        anchors.bottomMargin: 4;
        onColorPicked: {
            coloredText.color=clr
        }
    }


}
```

### 在ECMAScript 中动态创建对象

#### 从组件文件动态创建Component

```
Window {
    id: rootItem
    visible: true
    width: 640
    height: 480
    title: qsTr("Hello World")
    property var count: 0
    property Component component: null

    Text {
        id: coloredText;
        text: qsTr("Hello World");
        anchors.centerIn: parent;
        anchors.top:parent.top;
        anchors.topMargin: 4;
        font.pixelSize: 32;
    }
    Button{
        id: add;
        text:"add";
        anchors.left:parent.left;
        anchors.leftMargin: 4;
        anchors.bottom: parent.bottom;
        anchors.bottomMargin: 4;

        onClicked: {
            createColorPicker(Qt.rgba(Math.random(),Math.random(),Math.random(),1));
        }
    }

    function setTextColor(clr){
        coloredText.color=clr;
    }
    function createColorPicker(clr){
        if (rootItem.component==null){
            rootItem.component=Qt.createComponent("ColorPicker.qml");
        }
        var colorPicker;
        if(rootItem.component.status==Component.Ready){
            // 创建对象
            colorPicker=rootItem.component.createObject(rootItem,
               {"color":clr,"x":rootItem.count *55,"y":10})
            // 接收信号
            colorPicker.colorPicked.connect(rootItem.setTextColor);
        }
        if (rootItem.count%2==1){
           // 1秒后删除对象
            colorPicker.destroy(1000);
        }
        rootItem.count++;
    }
}
```

#### 从QML字符串动态创建Component

var newObj = Qt.createQmlObject('import QtQuick 2.12;
    Rectangle {color: "red";width:20; height:20}',parentItem,"dynamicSnippet1");

每一个参数是QML字符串,第二个参数用于指定要创建的对象的父对象，第三个参数用于给新创建的对象关联一个文件路径

#### 销毁EMCAScript 动态创建的对象

要删除一个对象，可以调用其destroy()方法

如之间例子中的：

colorPicker.destroy(1000);







