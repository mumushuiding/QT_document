## 信号与槽

### 组件默认定义的信号

点击button，改变text内容：

```
    Text {
        id: hello;
        text: qsTr("Hello World!");
        anchors.centerIn: parent;
        color: "blue";
        font.pixelSize: 32;
        onTextChanged: {
            console.log(text);
        }
    }
    Button{
        anchors.top:hello.bottom;
        anchors.topMargin: 8;
        anchors.horizontalCenter: parent.horizontalCenter;
        text:"change hello";
        onClicked: {
            hello.text="Hello QT Quick";
        }
    }
```

### 自定义信号

添加信号语法：

signal <name> [(<type> <parameter name> [,...]])]

```
Text {
        id: coloredText;
        text: qsTr("Hello world");
        anchors.horizontalCenter: parent.horizontalCenter;
        anchors.top: parent.top;
        anchors.topMargin: 4;
        font.pixelSize: 32;
    }
    Component{
        id: colorC;
        Rectangle{
            id: colorPicker;
            width: 50;
            height: 30;
            signal colorPicked(color clr); // 定义了信号colorPicked
            MouseArea{
                anchors.fill: parent
                // 当鼠标点击时触发了colorPicked 信号
                onPressed: colorPicker.colorPicked(colorPicker.color);
            }
            // 信号处理器，每个继承该组件的Loader都会执行
            onColorPicked:{
                console.log("pick color")
            }
        }
    }
    Loader{
        id: redLoader;
        anchors.left: parent.left;
        anchors.leftMargin: 4;
        anchors.bottom: parent.bottom;
        anchors.bottomMargin: 4;
        sourceComponent: colorC;
        onLoaded: {
            item.color = "red";
        }
    }
    Loader{
        id: blueLoader;
        anchors.left: redLoader.right;
        anchors.leftMargin: 4;
        anchors.bottom: parent.bottom;
        anchors.bottomMargin: 4;
        sourceComponent: colorC;
        onLoaded: {
            item.color = "blue";
        }
    }
    // 通过connections对象接收信号
    Connections{
        target: redLoader.item;
        // 接收colorPicked信号
        onColorPicked:{
            console.log("pick red");
            coloredText.color=clr;
        }
    }
    Connections{
        target: blueLoader.item;
        onColorPicked:{
            console.log("pick blue");
            coloredText.color=clr;
        }

    }

```

### 一个信号连接多个方法

```
Window {
    visible: true
    width: 640
    height: 480
    title: qsTr("Hello World")
    id: relay

    signal messageReceived(string notice);

    Component.onCompleted: {
        relay.messageReceived.connect(send2Post)
        relay.messageReceived.connect(send2Email)
    }
    function send2Post(notice){
        console.log("Post:" + notice)
    }
    function send2Email(notice){
        console.log("Email:" + notice)
    }
    Button {
        id: sendButton
        x: 12
        y: 16
        text: "sendMessage"
        checkable: true
        onClicked: {
            relay.messageReceived("Send Message")
        }
    }
```

